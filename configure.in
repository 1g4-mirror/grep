# Configuration for grep
#
# Alain Magloire <alainm@rcsm.ee.mcgill.ca>
#
dnl Process this file with autoconf to produce a configure script
AC_INIT(src/grep.c)
AC_DEFINE(GREP)
AC_PREREQ(2.12)

dnl Automake stuff.
AM_INIT_AUTOMAKE(grep, 2.2e)
AM_CONFIG_HEADER(config.h:config.hin)

dnl Checks for programs.
AC_PROG_AWK
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_RANLIB

# From Paul Eggert.
# If available, prefer support for large files unless the user specified
# one of the CPPFLAGS, LDFLAGS, or LIBS variables.
AC_LFS

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_SIZE_T
AC_C_CONST

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(string.h stdlib.h sys/param.h memory.h unistd.h libintl.h)
AC_CHECK_HEADERS(wctype.h wchar.h, USE_WCHAR=yes, USE_WCHAR=no)
AC_HEADER_DIRENT
AC_HEADER_STAT

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_FUNC_CLOSEDIR_VOID
AC_FUNC_MMAP
dnl getpagesize is checked for by AC_FUNC_MMAP.
AC_CHECK_FUNCS(strerror isascii setmode)
AC_REPLACE_FUNCS(memchr stpcpy)
dnl This test replaces the obsolescent AC_ISC_POSIX kludge.
dnl AC_CHECK_LIB(cposix, strerror, [LIBS="$LIBS -lcposix"])
dnl Solaris put it in /usr/lib/libw.a
AC_CHECK_LIB(w, wctype, [LIBS="$LIBS -lw"])
dnl Solaris 2.5 don't define btowc() in the wchar.h
if test "$USE_WCHAR" = "yes"; then
  AC_REPLACE_FUNCS(btowc)
fi

dnl I18N
ALL_LINGUAS="de es el fr ko nl no pl ru sl sv"
AM_GNU_GETTEXT
AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")

dnl for future try to integrate cygwin and djpp
dnl to make it less painful for modification.
AM_CYGWIN32

if test -d '.\.'
then
	AC_DEFINE(HAVE_DOS_FILE_NAMES)
fi

# Path separator for DOS/DJGPP
if test -n "$COMSPEC$ComSpec"
then
	AC_SUSBT(SEP, ';')
else
	AC_SUSBT(SEP, ':')
fi

dnl some folks ask for this, that's fine by me
dnl hope they know what they're doing ...
AC_ARG_WITH(included-regex,
  [  --without-included-regex         don't compile regex],
	USE_REGEX=$withval, USE_REGEX=yes)
test "$USE_REGEX" = "yes" && LIBOBJS="$LIBOBJS regex.o"

AC_OUTPUT(Makefile src/Makefile tests/Makefile po/Makefile.in intl/Makefile doc/Makefile m4/Makefile djgpp/Makefile vms/Makefile, [sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile; echo timestamp > stamp-h])
